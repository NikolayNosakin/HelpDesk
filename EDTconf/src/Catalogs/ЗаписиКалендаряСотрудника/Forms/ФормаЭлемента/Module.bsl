
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) И Не ЗначениеЗаполнено(Объект.Источник) Тогда
		СоздатьЭлементыРасширенногоВвода();
		КлючСохраненияПоложенияОкна = "РасширенныйВвод";
	КонецЕсли;
	
	СобытияCRMКлиентСервер.ЗаполнитьСписокВыбораВремени(Элементы.НачалоВремя);
	СобытияCRMКлиентСервер.ЗаполнитьСписокВыбораВремени(Элементы.ОкончаниеВремя);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ПометитьНаУдалениеПриЗаписи) Тогда
		ПометитьНаУдалениеПриЗаписи.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
		ПометитьНаУдалениеПриЗаписи = Справочники.ЗаписиКалендаряСотрудника.ПустаяСсылка();
	КонецЕсли;
	
	Если Не Отказ И значениеЗаполнено(ТекущийОбъект.Источник) Тогда
		Если ТекущийОбъект.ЭтоНовый() Тогда
			МенеджерЗаписи = РегистрыСведений.ПланированиеЗаявок.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Заявка = ТекущийОбъект.Источник;
			МенеджерЗаписи.ЗаписьКалендаря = ТекущийОбъект.Ссылка; 

			МенеджерЗаписи.ДатаНачала = ТекущийОбъект.Ссылка.Начало;
			МенеджерЗаписи.ДатаОкончания = ТекущийОбъект.Ссылка.Окончание;
			МенеджерЗаписи.Календарь = ТекущийОбъект.Ссылка.Календарь;

			МенеджерЗаписи.Записать();
		Иначе //Удаляем запись по предыдущей ссылке и создаем новую
			МенеджерЗаписи = РегистрыСведений.ПланированиеЗаявок.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Заявка = ТекущийОбъект.Ссылка.Источник;
			МенеджерЗаписи.ЗаписьКалендаря = ТекущийОбъект.Ссылка; 
		   	МенеджерЗаписи.Прочитать();
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Удалить();
			КонецЕсли;
			
			МенеджерЗаписи = РегистрыСведений.ПланированиеЗаявок.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Заявка = ТекущийОбъект.Источник;
			МенеджерЗаписи.ЗаписьКалендаря = ТекущийОбъект.Ссылка;
			МенеджерЗаписи.ДатаНачала = ТекущийОбъект.Ссылка.Начало;
			МенеджерЗаписи.ДатаОкончания = ТекущийОбъект.Ссылка.Окончание;
			МенеджерЗаписи.Календарь = ТекущийОбъект.Ссылка.Календарь;

			МенеджерЗаписи.Записать();
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// УНФ.КалендарьСотрудника
	Оповестить("Запись_ИсточникЗаписейКалендаряСотрудника");
	// Конец УНФ.КалендарьСотрудника
	
	Если ТипЗнч(Объект.Источник) = Тип("ДокументСсылка.Заявка") Тогда
		Оповестить("ЗаписаноИзменениеЗаявки");
	КонецЕсли;	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КалендарьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Идентификатор)
		И Не ЗначениеЗаполнено(ПометитьНаУдалениеПриЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Календарь = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПодменитьОбъектНаСервере(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыбранноеЗначение = НачалоДня(Объект.Начало) + (ВыбранноеЗначение - НачалоДня(ВыбранноеЗначение));
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыбранноеЗначение = НачалоДня(Объект.Окончание) + (ВыбранноеЗначение - НачалоДня(ВыбранноеЗначение));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьЭлементыРасширенногоВвода()
	
	ЗначениеВРеквизитФормы(Справочники.ЗаписиКалендаряСотрудника.ОписаниеРасширенногоВводаЗаписей(), "ОписаниеРасширенногоВвода");
	
	Для Индекс = 0 По ОписаниеРасширенногоВвода.Количество()-1 Цикл
		
		СтрокаОписания = ОписаниеРасширенногоВвода[Индекс];
		
		ГруппаРодитель = ?(Индекс <= ОписаниеРасширенногоВвода.Количество() / 2, Элементы.Колонка_1, Элементы.Колонка_2);
		
		ИмяЭлемента = "ВводТипа_" + Индекс;
		ДекорацияНадпись = Элементы.Добавить(ИмяЭлемента, Тип("ДекорацияФормы"), ГруппаРодитель);
		ДекорацияНадпись.Вид = ВидДекорацииФормы.Надпись;
		ДекорацияНадпись.Заголовок = СтрокаОписания.Представление;
		ДекорацияНадпись.Гиперссылка = Истина;
		ДекорацияНадпись.УстановитьДействие("Нажатие", "Подключаемый_ВводТипаНажатие");
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВводТипаНажатие(Элемент)
	Отказ = Ложь;
	
	Если не ЗначениеЗаполнено(Объект.Календарь) Тогда
		Сообщить("Не указан календарь пользователя!");
		Отказ = истина;
	КонецЕсли;
	
	Если не ЗначениеЗаполнено(Объект.Начало) Тогда
		Сообщить("Не указано время начала!");
		Отказ = истина;
	КонецЕсли;

	Если не ЗначениеЗаполнено(Объект.Окончание) Тогда
		Сообщить("Не указано время окончания!");
		Отказ = истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	

	ДанныеЗаписиКалендаря = Новый Структура;
	ДанныеЗаписиКалендаря.Вставить("Наименование",	Объект.Наименование);
	ДанныеЗаписиКалендаря.Вставить("Календарь",		Объект.Календарь);
	ДанныеЗаписиКалендаря.Вставить("Начало",		Объект.Начало);
	ДанныеЗаписиКалендаря.Вставить("Окончание",		Объект.Окончание);
	ДанныеЗаписиКалендаря.Вставить("Описание",		Объект.Описание);
	
	Индекс = Число(Сред(Элемент.Имя, СтрДлина("ВводТипа_")+1));
	СтрокаОписания = ОписаниеРасширенногоВвода[Индекс];
	
	Если Не СтрокаОписания.ПараметрыФормы.Свойство("ЗначенияЗаполнения") Тогда
		СтрокаОписания.ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура);
	КонецЕсли;
	
	СтрокаОписания.ПараметрыФормы.ЗначенияЗаполнения.Вставить("ДанныеЗаписиКалендаря", ДанныеЗаписиКалендаря);
	
	ОткрытьФорму(СтрокаОписания.ИмяФормы, СтрокаОписания.ПараметрыФормы);
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

// Процедура обеспечивает подмену объекта при изменении календаря.
// Такое поведение нужно для корректной выгрузки в Google записей,
// которые были перемещены между календарями.
&НаСервере
Процедура ПодменитьОбъектНаСервере(НовыйКалендарь)
	
	СохраняемыеСвойства = Новый Структура("Наименование,Начало,Окончание,Источник,НомерСтрокиИсточника,Описание");
	ЗаполнитьЗначенияСвойств(СохраняемыеСвойства, Объект);
	
	Модифицированность = Истина;
	
	Если ЗначениеЗаполнено(ПометитьНаУдалениеПриЗаписи)
		И ОбщегоНазначения.ПолучитьЗначениеРеквизита(ПометитьНаУдалениеПриЗаписи, "Календарь") = НовыйКалендарь Тогда
		
		// Вернули календарь к первоначальному значению
		ПредыдущаяЗаписьКалендаря = ПометитьНаУдалениеПриЗаписи.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ПредыдущаяЗаписьКалендаря, "Объект");
		ЗаполнитьЗначенияСвойств(Объект, СохраняемыеСвойства);
		ПометитьНаУдалениеПриЗаписи = Справочники.ЗаписиКалендаряСотрудника.ПустаяСсылка();
		Возврат;
		
	КонецЕсли;
	
	НоваяЗаписьКалендаря = Справочники.ЗаписиКалендаряСотрудника.СоздатьЭлемент();
	ПометитьНаУдалениеПриЗаписи = Объект.Ссылка;
	ЗначениеВРеквизитФормы(НоваяЗаписьКалендаря, "Объект");
	ЗаполнитьЗначенияСвойств(Объект, СохраняемыеСвойства);
	Объект.Календарь = НовыйКалендарь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникПриИзменении(Элемент)
	Объект.Наименование = ПолучитьНаименованиеИзОбъекта(Объект.Источник);
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	 Элементы.Наименование.Доступность = Не (ЗначениеЗаполнено(Объект.Источник));
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимость();
КонецПроцедуры

&НаСервере
Функция ПолучитьНаименованиеИзОбъекта(СсылкаНаИсточник)
	
	ПредставлениеЗаписи = СтрШаблон(
	НСтр("ru='Заявка №%1: %2';uk='Заявка №%1: %2'"),
	СсылкаНаИсточник.Номер,СсылкаНаИсточник.Тема
	);
	
	Возврат ПредставлениеЗаписи;
КонецФункции

&НаКлиенте
Процедура ИсточникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("ТекущаяСтрока",Объект.Источник);
	ОткрытьФорму("Документ.Заявка.Форма.ФормаВыбораМоиЗаявки",СтруктураПараметров,Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИсточникОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если Объект.Источник = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
