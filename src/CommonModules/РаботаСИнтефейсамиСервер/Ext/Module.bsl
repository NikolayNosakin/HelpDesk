Процедура ВыполнитьНастройкуПанелей() Экспорт
	
	НастройкиИнтерфейса = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиИнтерфейсаКлиентскогоПриложения");
	
	Если  НастройкиИнтерфейса = Неопределено Тогда
		 НастройкиИнтерфейса = Новый НастройкиИнтерфейсаКлиентскогоПриложения;
	КонецЕсли;
	
	// С помощью пустого объекта настроек получить настройки состава, установленные в конфигураторе.
	НастройкиСостава = НастройкиИнтерфейса.ПолучитьСостав();
	
	// Очистить настройки состава.
	НастройкиСостава.Верх.Очистить();
	НастройкиСостава.Лево.Очистить();
	НастройкиСостава.Низ.Очистить();
	НастройкиСостава.Право.Очистить();
	
	// Заполнить своими панелями.
	// Для примера самый простой вариант: слева - панель инструментов.
	ПанельИнструментов = Новый ЭлементНастройкиСоставаИнтерфейсаКлиентскогоПриложения("ПанельИнструментов");
	ПанельОткрытых = Новый ЭлементНастройкиСоставаИнтерфейсаКлиентскогоПриложения("ПанельОткрытых");
	ПанельРазделов = Новый ЭлементНастройкиСоставаИнтерфейсаКлиентскогоПриложения("ПанельРазделов");
	ПанельВверх = Новый ГруппаНастройкиСоставаИнтерфейсаКлиентскогоПриложения;
	
	ПанельВверх.Добавить(ПанельИнструментов);
	
	Если Не РольДоступна("НеОтображатьПанельРазделов") Тогда
		ПанельВверх.Добавить(ПанельРазделов);
	КонецЕсли;
	
	НастройкиСостава.Низ.Добавить(ПанельОткрытых);
	НастройкиСостава.Верх.Добавить(ПанельВверх);
	
	// Загрузить настройки состава в пустой объект настроек.
	НастройкиИнтерфейса.УстановитьСостав(НастройкиСостава);
	
	// Положить объект настроек текущему пользователю в хранилище.
	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиИнтерфейсаКлиентскогоПриложения", , НастройкиИнтерфейса);

	НастройкиКлиента = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения");
	Если  НастройкиКлиента = Неопределено Тогда
		 НастройкиКлиента = Новый НастройкиКлиентскогоПриложения;
	 КонецЕсли;
	 
	НастройкиКлиента.ОтображатьПанелиНавигацииИДействий = Ложь;
	НастройкиКлиента.ОтображатьПанельРазделов = Истина;
	НастройкиКлиента.РежимОткрытияФормПриложения = РежимОткрытияФормПриложения.Закладки;
	НастройкиКлиента.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения.Такси;

	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиКлиентскогоПриложения", "",
			НастройкиКлиента);

	НастройкиКомИнтерфейса =  ХранилищеСистемныхНастроек.Загрузить("Общее/ПанельРазделов/НастройкиКомандногоИнтерфейса");  
	Если  НастройкиКомИнтерфейса = Неопределено Тогда
		 НастройкиКомИнтерфейса = Новый НастройкиКомандногоИнтерфейса;
	 КонецЕсли;
	 
	НастройкиКомИнтерфейса.ОтображениеПанелиРазделов = ОтображениеПанелиРазделов.КартинкаИТекст;
	ХранилищеСистемныхНастроек.Сохранить("Общее/ПанельРазделов/НастройкиКомандногоИнтерфейса", "",
			НастройкиКомИнтерфейса);
			
КонецПроцедуры
